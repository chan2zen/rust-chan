# Rust 实现缠论(缠中说禅)的通达信插件

# 使用方法

```shell
rustup target add  i686-pc-windows-msvc
cargo build --release --target i686-pc-windows-msvc
copy .\target\i686-pc-windows-msvc\release\rust_chan.dll C:\tdx\T0002\dlls
```

## 通达信公式
绑定到第一个dll，并编辑公式如下:
```
FRAC:TDXDLL1(4, H, L, 1);

MH:=TDXDLL1(3, H, L, 1);
ML:=TDXDLL1(3, H, L, -1);

CH:IF(MH>0, MH, H);
CL:IF(ML>0, ML, L);
DUAN:TDXDLL1(5, FRAC, CH, CL);

DRAWLINE(FRAC=-1,CL,FRAC=+1,CH,0), DOTLINE, COLORYELLOW;
DRAWLINE(FRAC=+1,CH,FRAC=-1,CL,0), DOTLINE, COLORYELLOW;

BISE:=TDXDLL1(6, FRAC, CH, CL);
BIZG:=TDXDLL1(7, FRAC, CH, CL);
BIZD:=TDXDLL1(8, FRAC, CH, CL);
NOTEXT_BIZG:IF(BIZG>0,BIZG,DRAWNULL),COLORYELLOW;
NOTEXT_BIZD:IF(BIZD>0,BIZD,DRAWNULL),COLORYELLOW;
NOTEXT_BISE:STICKLINE(BISE>0,BIZD,BIZG,0,0),COLORYELLOW;

DRAWLINE(DUAN=-1,CL,DUAN=+1,CH,0), LINETHICK2, COLORFF8000;
DRAWLINE(DUAN=+1,CH,DUAN=-1,CL,0), LINETHICK2, COLORFF8000;

DRAWLINE(DUAN=-1,CL,DUAN=+2,CH,0), LINETHICK2, COLORLIBLUE;
DRAWLINE(DUAN=+1,CH,DUAN=-2,CL,0), LINETHICK2, COLORLIBLUE;

DUANZG1:=TDXDLL1(7,DUAN,CH,CL);
DUANZD1:=TDXDLL1(8,DUAN,CH,CL);
DUANSE1:=TDXDLL1(6,DUAN,CH,CL);

NOTEXT_DDUANZG1:IF(DUANZG1>0,DUANZG1,DRAWNULL),COLORFF8000;
NOTEXT_DDUANZD1:IF(DUANZD1>0,DUANZD1,DRAWNULL),COLORFF8000;

BUY1S:TDXDLL1(10, FRAC, CH, CL);
BUY1B:TDXDLL1(9, FRAC, CH, CL);

一买:BUY1B==1 OR BUY1S==1;
一卖:BUY1B==-1 OR BUY1S==-1;
三买:BUY1B==3 OR BUY1S==3 OR BUY1B=23 OR BUY1S=23;
三卖:BUY1B==-3 OR BUY1S==-3 OR BUY1B=-23 OR BUY1S=-23;
二买:BUY1B==2 OR BUY1S==2 OR BUY1B=23 OR BUY1S=23;
二卖:BUY1B==-2 OR BUY1S==-2 OR BUY1B=-23 OR BUY1S=-23;

笔幅:IF(FRAC=-1, REF(H, BARSLAST(FRAC=1)) - L, IF(FRAC=1, H - REF(L, BARSLAST(FRAC=-1)), DRAWNULL));
笔天数:IF(FRAC=-1, BARSLAST(FRAC=1) + 1 , IF(FRAC=1, BARSLAST(FRAC=-1) + 1, DRAWNULL));
笔力度:IF(FRAC=1 OR FRAC=-1, 笔幅/笔天数, DRAWNULL);
笔K数:IF(REF(BARSLAST(FRAC=-1),1) < REF(BARSLAST(FRAC=1),1), COUNT(CL <> REF(CL, 1), BARSLAST(FRAC=-1)) + 1, COUNT(CH <> REF(CH, 1), BARSLAST(FRAC=1)) + 1);

段幅:IF(DUAN=-1, REF(H, BARSLAST(DUAN=1)) - L, IF(DUAN=1, H - REF(L, BARSLAST(DUAN=-1)), DRAWNULL));
段天数:IF(DUAN=-1, BARSLAST(DUAN=1) + 1 , IF(DUAN=1, BARSLAST(DUAN=-1) + 1, DRAWNULL));
段力度:IF(DUAN=1 OR DUAN=-1, 段幅/段天数, DRAWNULL);
段K数:IF(REF(BARSLAST(DUAN=-1),1) < REF(BARSLAST(DUAN=1),1), COUNT(CL <> REF(CL, 1), BARSLAST(DUAN=-1)) + 1, COUNT(CH <> REF(CH, 1), BARSLAST(DUAN=1)) + 1);

前笔天数:=IF(FRAC=-1, REF(笔天数, REF(BARSLAST(FRAC=-1), BARSLAST(FRAC=1)) + BARSLAST(FRAC=1)), REF(笔天数, REF(BARSLAST(FRAC=1), BARSLAST(FRAC=-1)) + BARSLAST(FRAC=-1)));
前笔幅:=IF(FRAC=-1, REF(笔幅, REF(BARSLAST(FRAC=-1), BARSLAST(FRAC=1)) + BARSLAST(FRAC=1)), REF(笔幅, REF(BARSLAST(FRAC=1), BARSLAST(FRAC=-1)) + BARSLAST(FRAC=-1)));
前笔力度:=IF(FRAC=-1, REF(笔力度, REF(BARSLAST(FRAC=-1), BARSLAST(FRAC=1)) + BARSLAST(FRAC=1)), REF(笔力度, REF(BARSLAST(FRAC=1), BARSLAST(FRAC=-1)) + BARSLAST(FRAC=-1)));
前段天数:=IF(DUAN=-1, REF(段天数, REF(BARSLAST(DUAN=-1), BARSLAST(DUAN=1)) + BARSLAST(DUAN=1)), REF(段天数, REF(BARSLAST(DUAN=1), BARSLAST(DUAN=-1)) + BARSLAST(DUAN=-1)));
前段幅:=IF(DUAN=-1, REF(段幅, REF(BARSLAST(DUAN=-1), BARSLAST(DUAN=1)) + BARSLAST(DUAN=1)), REF(段幅, REF(BARSLAST(DUAN=1), BARSLAST(DUAN=-1)) + BARSLAST(DUAN=-1)));
前段力度:=IF(DUAN=-1, REF(段力度, REF(BARSLAST(DUAN=-1), BARSLAST(DUAN=1)) + BARSLAST(DUAN=1)), REF(段力度, REF(BARSLAST(DUAN=1), BARSLAST(DUAN=-1)) + BARSLAST(DUAN=-1)));
笔背离:笔天数 > 前笔天数 AND 笔幅 < 前笔幅 AND 笔力度 < 前笔力度;
段背离:段天数 > 前段天数 AND 段幅 < 前段幅 AND 段力度 < 前段力度;
```